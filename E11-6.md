# デジタル通信方式 I2C

前回まででデジタルの入出力方法、アナログの入出力方法を説明した。

ここではセンサーなどとの通信方法について説明する。

*Ref: 教科書 pp.42*

## I2C通信

I2C (正式にはアイスクエアドシーと読むが、少なくとも日本ではアイ・ツーシーでも通じる) は、IC 間で通信をするために開発された**デジタル通信方式**である。Arduino でも I2C 通信を用いて電子部品の制御ができる。ここでは I2C の概要と Arudino で I2C を利用するための手順について説明する。

### I2C通信の仕組み

I2C通信は、**マスター**と呼ばれるデバイスと**スレーブ**と呼ばれるデバイス間で行われる通信である。マスターは、通信を制御する役割を持ち、スレーブはマスターからの指示に従ってデータを送受信する。

かならず通信は **マスター** から始める。

<div style="text-align: center;">
  <img src="./images/image27.png" width="50%"><br/>
</div>

通信には、**SDA**（シリアルデータ）と**SCL**（シリアルクロック）の2本の信号線が使われる。

* **SDA:** データを送受信するための信号線
* **SCL:** 通信のタイミングを制御するためのクロック信号

マスターはSCL信号を生成し、スレーブはこの信号に同期して通信を行う。

**２本の線 (と GND) があれば事足りる、というのが最大の特徴**

<div style="text-align: center;">
  <img src="./images/image28.png" width="50%"><br/>
</div>

## マスターアドレスとスレーブアドレス

I2C のスレーブにはアドレスがついている。

- マスターはアドレスを指定して，スレーブに命令を伝える
    - マスター           「アドレス 04 番，応答せよ」
    - スレーブ（ンサー）  「はい！」

- **線上に同じアドレスのデバイスがあったらダメ**

### I2C通信の特徴

I2C通信には、以下のような特徴がある：

* **2線式:** SDAとSCLの2本の信号線だけで通信できるため、必要な配線が少なくて済む。
* **マルチマスタ:** 複数のマスターデバイスが同じバス上で通信することができる。
* **アドレス指定:** 各スレーブデバイスには固有のアドレスが割り当てられており、マスターはこのアドレスを使って特定のデバイスと通信することができる。
* **双方向通信:** マスターとスレーブの間で双方向にデータを送受信することができる。
* **オープンコレクタ:** SDAとSCL信号はオープンコレクタ出力であり、プルアップ抵抗が必要となる。

**利点:**

* 必要な配線が少なく、シンプルに接続できる
* 複数のデバイスを同じバス上で接続できる
* 低コストで実装できる
* 省電力

**欠点:**

* 通信速度が遅い
* ノイズの影響を受けやすい
* 長距離通信には向かない

# Arudino で I2C デバイスを使う

- Arduino で I2C デバイスと通信するには Wire ライブラリを使う。
- Arduino では SCL, SDA に使用できるピンが決まっている。

<div style="text-align: center;">
  <img src="./images/image29.png" width="50%"><br/>
</div>

# ADT7410 温度センサー

I2C 通信を利用する温度センサー ADT7410 がある。
これを使って、温度を読み取るプログラムを作ってみよう。

<div style="text-align: center;">
  <img src="./images/106675.jpg">
</div>

# 回路を作成する

- 回路を図を参考に、ADT7410 温度センサーを Arduino に接続する
  - VCC ⇒ 5V
  - GND ⇒ GND
  - SCL ⇒ AREF の２つ上 (図では緑)
  - SDA ⇒ AREF の１つ上 (図では黄色)

- この回路図とセンサーのピンの並びが異なるので注意。基盤に印刷されている文字に従うこと。

<div style="text-align: center;">
  <img src="./images/image30.png">
</div>

## I2C スキャナーを使って正しく接続されているか確認してみよう

- このページの下にある、I2C Scanner を使って温度センサーが正しく接続されているか確認する。
- 温度センサーのスレーブアドレスは 0x48 であるので、以下のようにシリアルモニタに表示されたら正しく接続されている。

> 15:45:00.420 -> I2C device found at address 0x48
> 15:45:00.904 -> I2C device found at address 0x48

## プログラム

I2C を使ってこのセンサーモジュールから温度を読み取るプログラムは以下のようになる：

**ここは読まなくていい：**

- **(A) の行について**  
  0. data[0] = 11111111 (255), data[1] = 01010101 (85) だったとする
  1. << という演算子は、<< 8 で、**左シフト** という作業をする (data[0] 左に 8 ビットずらす)  
  ⇒ 結果 11111111 00000000 なる。
  2. | という演算子は、OR 演算子で、data[0] をずらして空いた 8 ビットのところに data[1] を OR 演算している
  ⇒ 結果 11111111 01010101 (65365) という値になる
  3. それを rawTemperature に格納している

- **(B) の行について**

- このセンサーは *1bit* 変化すると *0.0625 ℃* 変化する

<div style="text-align: center;">
  <img src="./images/image32.png">
</div>


```c++
#include <Wire.h> // I2Cライブラリのインクルード

const int address = 0x48; // ADT7410のアドレス

void setup() {
  Serial.begin(9600); // シリアル通信の開始
  Wire.begin(); // I2C通信の開始
}

void loop() {
  // 温度データの読み取り
  byte data[2];
  Wire.requestFrom(address, 2);
  data[0] = Wire.read();
  data[1] = Wire.read();

  // 温度データの変換
  int rawTemperature = (data[0] << 8) | data[1]; // (A)
  rawTemperature = rawTemperature >> 3; // 下位 3 ビットは不要データ (捨てている)
  float temperature = rawTemperature * 0.0625; // 温度データの変換 (B)

  // 温度の表示
  Serial.print("Temperature: ");
  Serial.print(temperature);
  Serial.println("°C");

  delay(1000); // 1秒間隔で読み取り
}
```


# 有機ディスプレイをつないでみる

とにかく I2C の特徴は　**「配線が少なくてすむ」ということに尽きる。**

ここではもう一つの I2C デバイス、0.96 インチの有機 EL ディスプレイ　SSD1306 をつないで、そこに温度を表示させてみよう。

<div style="text-align: center;">
  <img src="./images/112031.jpg">
</div>


# 回路を作成する

- 回路を図を参考に、有機 EL ディスプレイ SSD1306 を接続する

  - VCC ⇒ 5V
  - GND ⇒ GND
  - SCL ⇒ ブレッドボード上で温度センサーの SCL と接続
  - SDA ⇒ ブレッドボード上で音素センサーの SDA と接続

- 回路図とセンサーのピンの並びが異なるので注意。基盤に印刷されている文字に従うこと。

<div style="text-align: center;">
  <img src="./images/image30.png">
</div>

## I2C スキャナーを使って正しく接続されているか確認する

- このページの下にある、I2C Scanner を使って温度センサーと有機 EL ディスプレイが正しく接続されているか確認する。
- 有機 EL ディスプレイのスレーブアドレスは 0x3C なので、以下の２行が表示されれば、正しく接続されている。

> 15:45:00.420 -> I2C device found at address 0x3C
> 15:45:00.904 -> I2C device found at address 0x48


## プログラム

### 前準備：ライブラリのインストール

SSD1306 を動かすには追加でライブラリのインストールが必要なので、まず、最初にこれをインストールする：

- Adafruit GFX ライブラリ
- Adafruit SSD1306  ライブラリ

Adafruit GFX ライブラリのインストール手順：

1. 画面左側の本が並んでいるのアイコンをクリックする
2. ライブラリマネージャーの検索欄に **Adafruit GFX** と入力する
3. 検索された Adafruit GFX Library をインストールする
4. 依存関係にあるライブラリもインストールする

**同じ要領で、Adafruit SSD1306 ライブラリもインストールする。**
**ライブラリのインストールは１回やれば、その後は不要。**


### ディスプレイに対する命令

- ディスプレイに対する命令は display.XXXXXX() という形をしている。
    - display.begin()
    - display.clearDisplay()
    - display.setTextSize()
    - display.setTextColor()
    - display.setCursor()
    - display.println()
    - display.display()

- ライブラリのマニュアルを調べて、どんな命令があるかを確認すればどのようなことができるかわかるが、必要なときに調べれば良い。

```c++
#include <Wire.h> // I2Cライブラリのインクルード
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>

#define SCREEN_WIDTH 128 // OLED display width, in pixels
#define SCREEN_HEIGHT 32 // OLED display height, in pixels
#define OLED_RESET     -1 // Reset pin # (or -1 if sharing Arduino reset pin)
#define SCREEN_ADDRESS 0x3C ///< See datasheet for Address; 0x3D for 128x64, 0x3C for 128x32

Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, OLED_RESET);

const int address = 0x48; // ADT7410のアドレス


void setup() {
  Serial.begin(9600); // シリアル通信の開始
  Wire.begin(); // I2C通信の開始
  
  // SSD1306_SWITCHCAPVCC = generate display voltage from 3.3V internally
  if(!display.begin(SSD1306_SWITCHCAPVCC, SCREEN_ADDRESS)) {
    Serial.println(F("SSD1306 allocation failed"));
    for(;;); // Don't proceed, loop forever
  }

  // 初期化画面を 1 秒表示する
  display.display();
  delay(1000);

  // ディスプレイをクリア
  display.clearDisplay();
  display.display();
}


void loop() {
  // 温度データの読み取り
  byte data[2];
  Wire.requestFrom(address, 2);
  data[0] = Wire.read();
  data[1] = Wire.read();

  // 温度データの変換
  int rawTemperature = (data[0] << 8) | data[1]; // (A)
  rawTemperature = rawTemperature >> 3; // 下位 3 ビットは不要データ (捨てている)
  float temperature = rawTemperature * 0.0625; // 温度データの変換 (B)

  // 温度の表示
  Serial.print("Temperature: ");
  Serial.print(temperature);
  Serial.println("°C");

  display.clearDisplay();                   // ディスプレイをクリア
  display.setTextSize(2);                   // 文字の大きさを設定
  display.setTextColor(SSD1306_WHITE);      // 文字の色を設定
  display.setCursor(0, 0);                  // 左上 (0, 0) の位置から文字を書く
  display.println( "Temp:" );               // Temp: と表示し、改行
  display.println( temperature );           // 温度を表示し、改行
  display.display();                        // 画面に描画する

  delay(1000); // 1秒間隔で読み取り
}
```

<div style="text-align: center;">
  <img src="./images/IMG20240430165206.jpg" width="50%">
</div>

## 実行結果


# I2C Scanner (I2C スキャナー)

いまどんな I2C デバイスが Arduino に接続されているか、知りたい時がある。

接続されている I2C デバイスを調べて表示するプログラムを一般的に I2C Scanner と呼ぶ。スキャン結果をシリアルモニタに出力することで、接続されているデバイスのアドレスを確認することができる。

1. 以下のプログラムをArduino IDEのスケッチエディタに貼り付ける。  
  このプログラムはすべてのスレーブアドレス (1~127 まで) について、スレーブがつながっているか調べるプログラム。

```c++
/*
 * I2C Scanner
 *
 * このスケッチは、I2Cバスに接続されているデバイスをスキャンします。
 * スキャン結果をシリアルモニタに出力します。
 *
 * Author: unknown
 */

#include <Wire.h>

void setup() {
  Serial.begin(9600);
  Serial.println("I2C Scanner");

  Wire.begin();
}

void loop() {
  byte address;

  for (address = 1; address < 128; address++) {
    Wire.beginTransmission(address);
    if (Wire.endTransmission() == 0) {
      Serial.print("I2C device found at address 0x");
      Serial.print(address, HEX);
      Serial.println();
    }
  }

  delay(500);
}
```

2. **スケッチをArduinoボードに書き込む。**
3. **Arduino IDEのシリアルモニタを開く。**
4. シリアルモニタに、I2Cバスに接続されているデバイスのアドレスが表示される。

**例**

```
I2C Scanner
I2C device found at address 0x48
```

この例では、I2Cバスにアドレス 0x48 のデバイスが接続されている。

### I2C Scannerの応用例

* I2Cバスに接続されているデバイスをデバッグする
* 新しいデバイスを接続したときに、アドレスを確認する
* I2Cバスのトラブルシューティングを行う